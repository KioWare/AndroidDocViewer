apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            storeFile file(localProps["storefile"])
            keyAlias localProps["keyalias"]
            storePassword localProps["storepassword"]
            keyPassword localProps["keypassword"]
        }
    }
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId "com.adsi.kioware.client.mobile.pdf.app"
        minSdkVersion 14
        targetSdkVersion 16
        versionName "1.1"
        versionCode computeVersionCode()
        signingConfig signingConfigs.config
    }
    buildTypes {
        release {
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    sourceSets {
        main.jni.srcDirs = []
        main.jniLibs.srcDir 'src/main/libs'
    }
    productFlavors {
    }
}

def computeVersionCode() {
    if (project.hasProperty('myVersionCode')) {
        return Integer.parseInt(project.myVersionCode)
    } else {
        return 10
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task ndkBuild(type: Exec) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkBuild = properties.getProperty('ndk.build')
    commandLine ndkBuild, '-C', file('src/main/jni').absolutePath
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}